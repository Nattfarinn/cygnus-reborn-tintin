#NOP MIT License
#NOP Copyright (c) 2023 Nattfarinn <nattfarinn@gmail.com>
#NOP See LICENCE file

#READ {cygnus-reborn-tintin/framework/msdp.tin};
#READ {cygnus-reborn-tintin/framework/screen.tin};
#READ {cygnus-reborn-tintin/framework/text.tin};
#READ {cygnus-reborn-tintin/framework/progressbar.tin};
#READ {cygnus-reborn-tintin/framework/fixtures.tin};

#FUNCTION {stat}
{
	#LOCAL {NAME}		%1;
	#LOCAL {BASE}		%2;
	#LOCAL {CURRENT}	%3;

	#LOCAL {BONUS}		0;
	#LOCAL {BONUS_COLOR}	<029>;
	#LOCAL {BONUS_TEXT}	{};

	#LOCAL {PADDING}	3;

	#MATH {BONUS} $CURRENT - $BASE;

	#IF {$BONUS < 0}
	{
		#LOCAL {BONUS_COLOR}	<010>;
	};

	#IF {$BONUS > 0}
	{
		#LOCAL {BONUS_TEXT}	@align_right{${PADDING};+${BONUS}};
	};
	#ELSEIF {$BONUS < 0}
	{
		#LOCAL {BONUS_TEXT}	@align_right{${PADDING};${BONUS}};
	};
	#ELSE
	{
		#LOCAL {BONUS_TEXT}	@align_right{${PADDING}};
	}

	#RETURN <159>${NAME}:<099> <ccc>${BASE}<099> ${BONUS_COLOR}${BONUS_TEXT}<099> <ccc>:<099> <179>${CURRENT}<099>;
};

#ALIAS {__DRAW_ALL}
{
	#SPLIT 1 3 0 40;
	#SCREEN CLEAR SQUARE 1 1 -1 -1;
	#BUFFER END;

	#DRAW <bbb> LINE -2 1 -2 -1;
	__DRAW_LOCAL;

	#DRAW <bbb> LINE 1 1 1 -1;
	__DRAW_EXPERIENCE;

	#DRAW <bbb> BOX 1 -40 10 -1;
	#SHOWME {<bbb>[@align_center{0;Character;1}]<099>} 1 -37;
	__DRAW_CHARACTER;

	#DRAW <bbb> BOX 10 -40 19 -1;
	#SHOWME {<bbb>[@align_center{0;Stats;1}]<099>} 10 -37;
	__DRAW_STATS;

	#DRAW <bbb> BOX 19 -40 40 -1;
	#SHOWME {<bbb>[@align_center{0;Affects;1}]<099>} 19 -37;
	__DRAW_AFFECTS;
};

#ALIAS {__DRAW_CHARACTER}
{
	#SCREEN CLEAR SQUARE 2 -39 9 -2;

	__DRAW_CHARACTER_LEVEL;
	__DRAW_CHARACTER_HP;
	__DRAW_CHARACTER_MP;
	__DRAW_CHARACTER_MV;
};

#ALIAS {__DRAW_CHARACTER_LEVEL}
{
	#SCREEN CLEAR SQUARE 4 -39 4 -2;

	#SHOWME {<179>$MSDP[CHARACTER_NAME]<099>} 3 -38;
	#SHOWME {<079>$MSDP[RACE] $MSDP[CLASS], Level $MSDP[LEVEL]<099>} 4 -38;
}

#ALIAS {__DRAW_CHARACTER_HP}
{
	#LOCAL {HEALTH_PERCENT}		0;
	#MATH {HEALTH_PERCENT} {($MSDP[HEALTH] * 100) / $MSDP[HEALTH_MAX]};
	#LOCAL {HEALTH_TEXT}		{$MSDP[HEALTH]/$MSDP[HEALTH_MAX], $HEALTH_PERCENT%};

	#SHOWME {HP: @progressbar{32;1;${HEALTH_PERCENT};@align_right{32;${HEALTH_TEXT};1}}} 6 -38;
};

#ALIAS {__DRAW_CHARACTER_MP}
{
	#LOCAL {MANA_PERCENT}		0;
	#MATH {MANA_PERCENT} {($MSDP[MANA] * 100) / $MSDP[MANA_MAX]};
	#LOCAL {MANA_TEXT}		{$MSDP[MANA]/$MSDP[MANA_MAX], $MANA_PERCENT%};

	#SHOWME {MP: @progressbar{32;4;${MANA_PERCENT};@align_right{32;$MANA_TEXT;1}}} 7 -38;
};

#ALIAS {__DRAW_CHARACTER_MV}
{
	#LOCAL {MOVEMENT_PERCENT}	0;
	#MATH {MOVEMENT_PERCENT} {($MSDP[MOVEMENT] * 100) / $MSDP[MOVEMENT_MAX]};
	#LOCAL {MOVEMENT_TEXT}		{$MSDP[MOVEMENT]/$MSDP[MOVEMENT_MAX], $MOVEMENT_PERCENT%};

	#SHOWME {MV: @progressbar{32;3;${MOVEMENT_PERCENT};@align_right{32;$MOVEMENT_TEXT;1}}} 8 -38;
};

#ALIAS {__DRAW_STATS}
{
	#SHOWME {@stat{STR;$MSDP[STR_PERM];$MSDP[STR]}} 12 -38;
	#SHOWME {@stat{INT;$MSDP[INT_PERM];$MSDP[INT]}} 13 -38;
	#SHOWME {@stat{WIS;$MSDP[WIS_PERM];$MSDP[WIS]}} 14 -38;
	#SHOWME {@stat{DEX;$MSDP[DEX_PERM];$MSDP[DEX]}} 15 -38;
	#SHOWME {@stat{CON;$MSDP[CON_PERM];$MSDP[CON]}} 16 -38;
	#SHOWME {@stat{LCK;$MSDP[LCK_PERM];$MSDP[LCK]}} 17 -38;
	#SHOWME {<159>@align_right{10;HitRoll:}<099> <179>@align_right{4;$MSDP[HITROLL];0}<099>} 12 -17;
	#SHOWME {<159>@align_right{10;DamRoll:}<099> <179>@align_right{4;$MSDP[DAMROLL];0}<099>} 13 -17;
	#SHOWME {<159>@align_right{10;SplPowr:}<099> <179>@align_right{4;-;0}<099>} 14 -17;
	#SHOWME {<159>@align_right{10;AC:}<099> <179>@align_right{4;$MSDP[AC];0}<099>} 15 -17;
	#SHOWME {<159>@align_right{10;Alignment:}<099> <179>@align_right{4;$MSDP[ALIGNMENT];0}<099>} 17 -17;
};

#FUNCTION {parse_csv_list}
{
	#LOCAL {INPUT}		%1;
	#LOCAL {IS_KEY}		1;
	#LOCAL {KEY}		{};
	#LOCAL {VALUE}		{};
	#LOCAL {TABLE}		{};
	#LOCAL {LIST}		{};

	#LIST {LIST} CREATE;

	#PARSE $INPUT {CHARACTER}
	{
		#IF {"$CHARACTER" != ","}
		{
			#IF {$IS_KEY == 1}
			{
				#CAT {KEY} $CHARACTER;
			};
			#ELSE 
			{
				#CAT {VALUE} $CHARACTER;
			};
		};
		#ELSE
		{
			#IF {$IS_KEY == 1}
			{
				#LOCAL {IS_KEY}		0;
			};
			#ELSE
			{
				#LOCAL {TABLE[NAME]}	$KEY;
				#LOCAL {TABLE[VALUE]}	$VALUE;
				#LIST {LIST} ADD {{$TABLE}};

				#LOCAL {KEY}		{};
				#LOCAL {VALUE}		{};
				#LOCAL {TABLE}		{};

				#LOCAL {IS_KEY}		1;
			};
		};
	};

	#RETURN $LIST;
};

#FUNCTION {parse_csv_table}
{
	#LOCAL {INPUT}		%1;
	#LOCAL {IS_KEY}		1;
	#LOCAL {KEY}		{};
	#LOCAL {VALUE}		{};
	#LOCAL {TABLE}		{};

	#PARSE $INPUT {CHARACTER}
	{
		#IF {"$CHARACTER" != ","}
		{
			#IF {$IS_KEY == 1}
			{
				#CAT {KEY} $CHARACTER;
			};
			#ELSE 
			{
				#CAT {VALUE} $CHARACTER;
			};
		};
		#ELSE
		{
			#IF {$IS_KEY == 1}
			{
				#LOCAL {IS_KEY}		0;
			};
			#ELSE
			{
				#LOCAL {TABLE[$KEY]}	$VALUE;
				#LOCAL {KEY}		{};
				#LOCAL {VALUE}		{};

				#LOCAL {IS_KEY}		1;
			};
		};
	};

	#RETURN $TABLE;
};

#FUNCTION {readable_time}
{
	#LOCAL {SECONDS}	%1;
	#LOCAL {MINUTES}	0;
	#LOCAL {HOURS}		0;
	#LOCAL {TIME}		{};

	#MATH {HOURS} {$SECONDS / 3600};
	#MATH {MINUTES} {($SECONDS - $HOURS * 3600) / 60};
	#MATH {SECONDS} {$SECONDS % 60};

	#FORMAT {TIME} {%+2s:%+2s:%+2s} {$HOURS} {$MINUTES} {$SECONDS};

	#REPLACE {TIME} { } {0};

	#RETURN $TIME;
};

#ALIAS {__DRAW_AFFECTS}
{
	#LOCAL {AFFECTS} 	@parse_csv_list{$MSDP[AFFECTS]};
	#LOCAL {POSITION}	21;

	#FOREACH {*AFFECTS[1..-1]} {INDEX}
	{
		#LOCAL {AFFECT}		$AFFECTS[$INDEX];
		#LOCAL {NAME}		$AFFECT[NAME];
		#LOCAL {TICKS}		$AFFECT[VALUE];
		#LOCAL {VALUE}		@readable_time{$TICKS};

		#LOCAL {COLOR}		<149>;

		#IF {$TICKS < 15}
		{
			#LOCAL {COLOR}	<549>;
		};

		#FORMAT {NAME} {%n} $NAME;

		#SHOWME {${COLOR}@align_left{24;$NAME}<099>@align_right{6;$TICKS} ticks} $POSITION -38;	

		#MATH {POSITION} {$POSITION + 1};
	};

	#SCREEN CLEAR SQUARE $POSITION -39 39 -2;
};

#ALIAS {__DRAW_LOCAL}
{
	__DRAW_LOCAL_TIME;
	__DRAW_LOCAL_LOCATION;
};

#ALIAS {__DRAW_LOCAL_TIME}
{
	#LOCAL {TIME}	0;	
	#FORMAT {TIME} {%+5s} $MSDP[WORLD_TIME];
	#REPLACE {TIME} { } {0};

	#SHOWME {<bbb>[<099><070>@align_center{0;$TIME;1}<099><bbb>]<099>} -2 4;
};

#ALIAS {__DRAW_LOCAL_LOCATION}
{
	#DRAW <bbb> LINE -2 15 -2 -1;

	#SHOWME {<bbb>[<099><070>@align_center{0;$MSDP[ROOM_NAME] ($MSDP[AREA_NAME]);1}<099><bbb>]<099>} -2 15;
};

#ALIAS {__DRAW_COMBAT}
{
	#LOCAL CHARACTER_NAME_LENGTH	0;
	#LOCAL WIDTH			76;
	#LOCAL START_X			0;
	#LOCAL START_Y			-4;
	#LOCAL END_X			0;
	#LOCAL END_Y			-2;
	#LOCAL START_CHARACTER_NAME	0;
	#LOCAL START_OPPONENT_NAME	0;
	#LOCAL OPPONENT_TEXT		{$MSDP[OPPONENT_NAME], Level $MSDP[OPPONENT_LEVEL]};
	#LOCAL OPPONENT_TEXT_LENGTH	0;
	#LOCAL HEALTH_PERCENT		0;
	#LOCAL OPPONENT_HEALTH_TEXT	{$MSDP[OPPONENT_HEALTH]%, Level $MSDP[OPPONENT_LEVEL]};

	#LOCAL BAR_WIDTH		0;
	#LOCAL START_CHARACTER_BAR	0;
	#LOCAL START_OPPONENT_BAR	0;

	
	#FORMAT {CHARACTER_NAME_LENGTH} {%L} $MSDP[CHARACTER_NAME];
	#FORMAT {OPPONENT_NAME_LENGTH} {%L} $MSDO[OPPONENT_NAME];

	#MATH {START_X}	{$SCREEN_WIDTH_CENTER - ($WIDTH / 2)};
	#MATH {END_X} {$START_X + $WIDTH};

	#MATH {START_CHARACTER_NAME} {$SCREEN_WIDTH_CENTER - $CHARACTER_NAME_LENGTH - 4 - 2};
	#MATH {START_OPPONENT_NAME} {$SCREEN_WIDTH_CENTER + 3};

	#MATH {HEALTH_PERCENT} {($MSDP[HEALTH] * 100) / $MSDP[HEALTH_MAX]};
	#LOCAL HEALTH_TEXT		{$MSDP[HEALTH]/$MSDP[HEALTH_MAX], $HEALTH_PERCENT%};

	#MATH {BAR_WIDTH} {($WIDTH - 5) / 2};
	#MATH {START_CHARACTER_BAR} {$START_X + 2};
	#MATH {START_OPPONENT_BAR} {$SCREEN_WIDTH_CENTER + 2};

	#IF {"$MSDP[OPPONENT_NAME]" != ""}
	{
		#DRAW <bbb> BOX $START_Y $START_X $END_Y $END_X;

		#SHOWME {<bbb>[<070>@align_center{0;$MSDP[CHARACTER_NAME];1}<bbb>]<099>} -4 $START_CHARACTER_NAME;
		#SHOWME {<bbb>[<070>@align_center{0;$MSDP[OPPONENT_NAME];1}<bbb>]<099>} -4 $START_OPPONENT_NAME;

		#SHOWME {@progressbar{$BAR_WIDTH;1;$HEALTH_PERCENT;@align_right{$BAR_WIDTH;$HEALTH_TEXT;1};1}} -3 $START_CHARACTER_BAR;
		#SHOWME {@progressbar{$BAR_WIDTH;1;$MSDP[OPPONENT_HEALTH];@align_left{0;$OPPONENT_HEALTH_TEXT;1}}} -3 $START_OPPONENT_BAR;
	};
	#ELSE
	{
		#SCREEN CLEAR SQUARE $START_Y $START_X $END_Y $END_X;
		__DRAW_LOCAL_LOCATION;
	};
};

#ALIAS {__DRAW_EXPERIENCE}
{
	#LOCAL WIDTH		75;
	#LOCAL START_X		0;
	#LOCAL END_X		0;
	#LOCAL EXPERIENCE	$MSDP[EXPERIENCE];
	#LOCAL EXPERIENCE_MAX	$MSDP[EXPERIENCE_MAX];

	#LOCAL BAR_WIDTH	0;
	#MATH {BAR_WIDTH} {$WIDTH - 6};

	#FORMAT {EXPERIENCE} {%g} $EXPERIENCE;
	#FORMAT {EXPERIENCE_MAX} {%g} $EXPERIENCE_MAX;

	#MATH {START_X}	{($SCREEN_WIDTH - $WIDTH) / 2};
	#MATH {END_X} {$START_X + $WIDTH};

	#LOCAL EXPERIENCE_TEXT	{$EXPERIENCE / $EXPERIENCE_MAX, $MSDP[EXPERIENCE_TNL]%%};
	#LOCAL {MANA_TEXT}		{$MSDP[MANA]/$MSDP[MANA_MAX], $MANA_PERCENT%};

	#DRAW <bbb> LINE 1 $START_X 1 $END_X;
	#SHOWME {<bbb>[<099> Exp: @progressbar{$BAR_WIDTH;2;$MSDP[EXPERIENCE_TNL];@align_left{0;$EXPERIENCE_TEXT;1}} <bbb>]<099>} 1 $START_X;
};

#ALIAS {__MSDP_DRAW}
{
	#LOCAL {VARIABLE}	%1;

	#SWITCH {"$VARIABLE"}
	{
		#CASE "CHARACTER_NAME"	__DRAW_CHARACTER_LEVEL;
		#CASE "CLASS"		__DRAW_CHARACTER_LEVEL;
		#CASE "RACE"		__DRAW_CHARACTER_LEVEL;
		#CASE "LEVEL"		__DRAW_CHARACTER_LEVEL;
		#CASE "HEALTH"		__DRAW_CHARACTER_HP;
		#CASE "HEALTH_MAX"	__DRAW_CHARACTER_HP;
		#CASE "MANA"		__DRAW_CHARACTER_MP;
		#CASE "MANA_MAX"	__DRAW_CHARACTER_MP;
		#CASE "MOVEMENT"	__DRAW_CHARACTER_MV;
		#CASE "MOVEMENT_MAX"	__DRAW_CHARACTER_MV;
		#CASE "STR"		__DRAW_STATS;
		#CASE "INT"		__DRAW_STATS;
		#CASE "WIS"		__DRAW_STATS;
		#CASE "DEX"		__DRAW_STATS;
		#CASE "CON"		__DRAW_STATS;
		#CASE "LCK"		__DRAW_STATS;
		#CASE "STR_PERM"	__DRAW_STATS;
		#CASE "INT_PERM"	__DRAW_STATS;
		#CASE "WIS_PERM"	__DRAW_STATS;
		#CASE "DEX_PERM"	__DRAW_STATS;
		#CASE "CON_PERM"	__DRAW_STATS;
		#CASE "LCK_PERM"	__DRAW_STATS;
		#CASE "AC"		__DRAW_STATS;
		#CASE "HITROLL"		__DRAW_STATS;
		#CASE "DAMROLL"		__DRAW_STATS;
		#CASE "SPLPOWR"		__DRAW_STATS;
		#CASE "ALIGNMENT"	__DRAW_STATS;
		#CASE "AFFECTS"		__DRAW_AFFECTS;
		#CASE "WORLD_TIME"	__DRAW_LOCAL_TIME;
		#CASE "AREA_NAME"	__DRAW_LOCAL_LOCATION;
		#CASE "ROOM_NAME"	__DRAW_LOCAL_LOCATION;
		#CASE "ROOM_VNUM"	__DRAW_LOCAL_LOCATION;
		#CASE "OPPONENT_HEALTH"	__DRAW_COMBAT;
		#CASE "OPPONENT_LEVEL"	__DRAW_COMBAT;
		#CASE "OPPONENT_NAME"	__DRAW_COMBAT;
		#CASE "EXPERIENCE"	__DRAW_EXPERIENCE;
		#CASE "EXPERIENCE_MAX"	__DRAW_EXPERIENCE;
		#CASE "EXPERIENCE_TNL"	__DRAW_EXPERIENCE;
	};
};

#ALIAS {__REDRAW}
{
	__DRAW_ALL;
};

#EVENT {SESSION CREATED}
{
	__DRAW_ALL;
};

